= Security

The following documentation provides context, reasoning, and examples of methods and constants found in `openzeppelin/security/`.

CAUTION: Expect this module to evolve.

== Initializable

The xref:api/security.adoc#Initializable[Initializable] component provides a simple mechanism that mimics
the functionality of a constructor.
More specifically, it enables logic to be performed once and only once which is useful to setup a contract's initial state when a constructor cannot be used, for example, when there are circular dependencies at construction time.

You can use the component in your contracts like this:

[,javascript]
----
#[starknet::contract]
mod MyInitializableContract {
    use openzeppelin::security::Initializable as initializable_component;

    component!(path: initializable_component, storage: initializable, event: InitializableEvent);

    impl InternalImpl = initializable_component::InternalImpl<ContractState>;

    #[storage]
    struct Storage {
        #[substorage(v0)]
        initializable: initializable_component::Storage,
        param: felt252
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        InitializableEvent: initializable_component::Event
    }

    fn initializer(ref self: ContractState, some_param: felt252) {
        // Makes the method callable only once
        self.initializable.initialize();

        // Initialization logic
        self.param.write(some_param);
    }
}
----

CAUTION: This Initializable pattern should only be used on one function.

== Pausable

:assert_not_paused: xref:api/security.adoc#Pausable-assert_not_paused[assert_not_paused]
:assert_paused: xref:api/security.adoc#Pausable-assert_paused[assert_paused]

The xref:api/security.adoc#Pausable[Pausable] component allows contracts to implement an emergency stop mechanism.
This can be useful for scenarios such as preventing trades until the end of an evaluation period or having an emergency switch to freeze all transactions in the event of a large bug.

To become pausable, the contract should include `pause` and `unpause` functions (which should be protected).
For methods that should be available only when paused or not, insert calls to `{assert_paused}` and `{assert_not_paused}`
respectively.

For example (using the xref:api/access.adoc#Ownable[Ownable] component for access control):

[,javascript]
----
#[starknet::contract]
mod MyPausableContract {
    use openzeppelin::access::ownable::Ownable as ownable_component;
    use openzeppelin::security::Pausable as pausable_component;
    use starknet::ContractAddress;

    component!(path: ownable_component, storage: ownable, event: OwnableEvent);
    component!(path: pausable_component, storage: pausable, event: PausableEvent);

    // Ownable
    #[abi(embed_v0)]
    impl OwnableImpl = ownable_component::OwnableImpl<ContractState>;
    impl OwnableInternalImpl = ownable_component::InternalImpl<ContractState>;

    // Pausable
    #[abi(embed_v0)]
    impl PausableImpl = pausable_component::PausableImpl<ContractState>;
    impl PausableInternalImpl = pausable_component::InternalImpl<ContractState>;

    #[storage]
    struct Storage {
        #[substorage(v0)]
        ownable: ownable_component::Storage,
        #[substorage(v0)]
        pausable: pausable_component::Storage
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        OwnableEvent: ownable_component::Event,
        PausableEvent: pausable_component::Event
    }

    #[constructor]
    fn constructor(ref self: ContractState, owner: ContractAddress) {
        self.ownable.initializer(owner);
    }

    #[generate_trait]
    #[external(v0)]
    impl External of ExternalTrait {
        fn pause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.pause();
        }

        fn unpause(ref self: ContractState) {
            self.ownable.assert_only_owner();
            self.pausable.unpause();
        }

        fn when_not_paused(ref self: ContractState) {
            self.pausable.assert_not_paused();
            // Do something
        }

        fn when_paused(ref self: ContractState) {
            self.pausable.assert_paused();
            // Do something
        }
    }
}
----

== Reentrancy Guard

:start: xref:api/security.adoc#ReentrancyGuard-start[start]
:end: xref:api/security.adoc#ReentrancyGuard-end[end]

A https://gus-tavo-guim.medium.com/reentrancy-attack-on-smart-contracts-how-to-identify-the-exploitable-and-an-example-of-an-attack-4470a2d8dfe4[reentrancy attack] occurs when the caller is able to obtain more resources than allowed by recursively calling a target's function.

Since Cairo does not support modifiers like Solidity, the xref:api/security.adoc#ReentrancyGuard[ReentrancyGuard]
component exposes two methods `{start}` and `{end}` to protect functions against reentrancy attacks.
The protected function must call `start` before the first function statement, and `end` before the return statement, as shown below:

[,javascript]
----
#[starknet::contract]
mod MyReentrancyContract {
    use openzeppelin::security::ReentrancyGuard as reentrancy_guard_component;

    component!(
        path: reentrancy_guard_component, storage: reentrancy_guard, event: ReentrancyGuardEvent
    );

    impl InternalImpl = reentrancy_guard_component::InternalImpl<ContractState>;

    #[storage]
    struct Storage {
        #[substorage(v0)]
        reentrancy_guard: reentrancy_guard_component::Storage
    }

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        ReentrancyGuardEvent: reentrancy_guard_component::Event
    }

    #[generate_trait]
    #[external(v0)]
    impl External of ExternalTrait {
        fn protected_function(ref self: ContractState) {
            self.reentrancy_guard.start();

            // Do something

            self.reentrancy_guard.end();
        }

        fn another_protected_function(ref self: ContractState) {
            self.reentrancy_guard.start();

            // Do something

            self.reentrancy_guard.end();
        }
    }
}
----

NOTE: The guard prevents the execution flow from ocurring inside `protected_function` to call `another_protected_function`,
and vice versa.